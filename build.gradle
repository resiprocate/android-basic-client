buildscript {
    repositories {
        mavenLocal()
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.2'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

group 'org.resiprocate.android'
version '1.0'

android {
    defaultConfig {
        versionCode 1
        versionName "1.0"

        minSdkVersion 21
        targetSdkVersion 30

        externalNativeBuild {
            cmake {
                arguments '-DANDROID_STL=c++_shared'
                cppFlags '-DUSE_SSL'
            }
        }
    }

    packagingOptions {
        doNotStrip '*/*/*.so'
    }

    compileSdkVersion 30
    buildToolsVersion "30.0.3"
    ndkVersion "23.1.7779620"

    //lintOptions {
    //    abortOnError false
    //}

    buildFeatures {
        prefab true
    }

    dependencies {
        implementation("com.android.ndk.thirdparty:openssl:1.1.1l-SNAPSHOT")
        implementation("org.resiprocate:resiprocate:1.13.0-SNAPSHOT")
    }

    buildTypes {
        distributable.initWith(buildTypes.release)
    }

    externalNativeBuild {
        cmake {
            version '3.18.1'
            path 'jni/CMakeLists.txt'
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            jni.srcDirs = ['jni']
            jniLibs.srcDir 'libs'
        }
    }
}

allprojects {
    repositories {
        mavenLocal()
        google()
        mavenCentral()
        maven {
            url "${rootProject.buildDir}/../../ndkports/build/repository"
        }
    }
}

publishing {
    repositories {
        maven {
            url "${rootProject.buildDir}/../build/repository"
        }
    }
    publications {
        maven(MavenPublication) {
            groupId 'org.resiprocate.android'
            artifactId 'android-basic-client'
            version '1.0'
            artifact("$buildDir/outputs/aar/android-basic-client-distributable.aar")
        }
    }
}
